apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.4.201502262128" //指定jacoco的版本
    reportsDir = file("$buildDir/JacocoReport") //指定jacoco生成报告的文件夹
}

android {
    buildTypes {
        debug {
            //打开覆盖率统计开关
            testCoverageEnabled = true
        }
    }
}

//首先先删除旧的merge结果文件
task removeOldMergeEc(type: Delete) {
    delete "$buildDir/mergedcoverage.ec"
}

task mergeReport(type: JacocoMerge, dependsOn:removeOldMergeEc){
    group = "reporting"
    description = "merge jacoco report."
    destinationFile= file("$buildDir/mergedcoverage.ec")
    //这里的ec_dir是存储ec文件的文件夹
    FileTree tree = fileTree("$buildDir/coverage") {
        include '**/*.ec'
    }
    executionData = tree
}

task jacocoTestReport(type: JacocoReport, dependsOn:mergeReport) {
    group = "reporting"
    description = "generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['**/R*.class',
                       '**/BuildConfig.*',
                       '**/*$InjectAdapter.class',
                       '**/*$ModuleAdapter.class',
                       '**/*$ViewInjector*.class',
                       '**/Manifest*.*'
            ])
    sourceDirectories = files("${project.projectDir}/src/main/java")
    executionData = files("$buildDir/mergedcoverage.ec")
}
